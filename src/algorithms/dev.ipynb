{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": 25,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "differential_basis"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "using AbstractAlgebra\n",
    "using AlgebraicSolving\n",
    "\n",
    "\"\"\"\n",
    "    _diff_op(q, derivatives)\n",
    "\n",
    "    This function evaluates the linear differential operator from \n",
    "    Definition 7 and Notation 3 in the thesis for a polynomial and derivatives.\n",
    "    The inputs should not explicitly depend on time.\n",
    "\n",
    "    # Arguments\n",
    "    - `q`: a polynomial\n",
    "    - `derivatives`: a dictionary of derivatives\n",
    "\n",
    "    # Returns\n",
    "    - the linear differential operator applied to the polynomial\n",
    "\"\"\"\n",
    "function _diff_op(q, derivatives)    \n",
    "    n = length(q.parent.data.S)\n",
    "    @assert length(derivatives) <= n \"There can't be more derivatives than variables.\"\n",
    "    \n",
    "    result = 0\n",
    "    for (var, value) in derivatives\n",
    "        result += value * AbstractAlgebra.derivative(q, var)\n",
    "    end\n",
    "    return result\n",
    "end\n",
    "\n",
    "\"\"\"\n",
    "    _intersect(G, S_vars)\n",
    "\n",
    "    This function computes the intersection of a Groebner basis \n",
    "    with a set of variables. It is important that a proper elimination\n",
    "    order is used to ensure that the variables that are to be eliminated\n",
    "    by intersection can actually be eliminated.\n",
    "\n",
    "    An example would be lexico-graphical ordering where the variables that are\n",
    "    to be eliminated are the first ones. Or alternatively and superior in\n",
    "    practice is to use block ordering where the variables that are to be\n",
    "    eliminated are in the larger block w.r.t. to the ordering.\n",
    "\n",
    "    # Arguments\n",
    "    - `G`: a Groebner basis w.r.t a monomial odering that eliminates all \n",
    "       variables except the ones in `S_vars`\n",
    "    - `S_vars`: the list of variables that are not to be eliminated\n",
    "\n",
    "    # Returns\n",
    "    - the intersection of the Groebner basis with the subring S\n",
    "\"\"\"\n",
    "function _intersect(G, S_vars)\n",
    "    sub_ideal = []\n",
    "    for generator in G\n",
    "        symbols = [Symbol(var) for var in AbstractAlgebra.vars(generator)]\n",
    "        if issubset(symbols, S_vars)\n",
    "            push!(sub_ideal, generator)\n",
    "        end\n",
    "    end\n",
    "    return sub_ideal\n",
    "end\n",
    "\n",
    "\"\"\"\n",
    "    _manage_rings(ideal, derivatives, R)\n",
    "\n",
    "    This function creates a new ring that permits the elimination of variables.\n",
    "\n",
    "    # Arguments\n",
    "    - `ideal`: an ideal\n",
    "    - `derivatives`: a dictionary of derivatives\n",
    "    - `R`: a polynomial ring with two blocks of variables, where the first \n",
    "       block corresponds to the variables in `R` that are not in `derivatives` \n",
    "       and the second block corresponds to the variables in `derivatives`\n",
    "\n",
    "    # Returns\n",
    "    - the new ring\n",
    "    - the new variables\n",
    "    - a map_old_new from the old variables to the new variables\n",
    "\"\"\"\n",
    "function _manage_rings(derivatives, R)\n",
    "    S_proper = []\n",
    "    R_elim = []\n",
    "    s = length(R.data.S) - length(derivatives)\n",
    "    map_old_new = Dict()\n",
    "    \n",
    "    index_n_el = s + 1\n",
    "    index_el = 1\n",
    "\n",
    "    # Move the variables to the correct blocks and create a map_old_new\n",
    "    for var in R.data.S\n",
    "        if Symbol(var) in [Symbol(key) for key in keys(derivatives)]\n",
    "            push!(S_proper, Symbol(var))\n",
    "            map_old_new[Symbol(var)] = index_n_el\n",
    "            index_n_el += 1\n",
    "        else    \n",
    "            push!(R_elim, Symbol(var))\n",
    "            map_old_new[Symbol(var)] = index_el\n",
    "            index_el += 1\n",
    "        end\n",
    "    end\n",
    "\n",
    "    # Create new ring, where the first block is the eliminated variables\n",
    "    R_vars_proper = append!(R_elim, S_proper)\n",
    "    R_new_vars = [string(var) for var in R_vars_proper]\n",
    "    R_new, R_new_vars = AlgebraicSolving.polynomial_ring(\n",
    "        base_ring(R), R_new_vars, internal_ordering=:degrevlex)\n",
    "    \n",
    "    return R_new, R_new_vars, map_old_new\n",
    "end\n",
    "\n",
    "\n",
    "\"\"\"\n",
    "    _swap_vars(poly, R_new, R_new_vars, map_old_new)\n",
    "\n",
    "    This function swaps the variables in a polynomial to a new ring.\n",
    "\n",
    "    # Arguments\n",
    "    - `poly`: a polynomial\n",
    "    - `R_new`: a new ring\n",
    "    - `R_new_vars`: the new variables\n",
    "    - `map_old_new`: a map_old_new from the old variables to the new variables\n",
    "\n",
    "    # Returns\n",
    "    - the polynomial in the new ring\n",
    "\"\"\"\n",
    "function _swap_vars(poly, R_vars, R_new_vars, map_old_new)\n",
    "    vars_subst = [R_new_vars[map_old_new[Symbol(var)]] for var in R_vars]\n",
    "    if poly == 0\n",
    "        return poly\n",
    "    end\n",
    "    poly_new = poly(vars_subst...)\n",
    "    return poly_new\n",
    "end\n",
    "\n",
    "\n",
    "\"\"\"\n",
    "    differential_basis(ideal, derivatives, R, nf=false, info_level=0)\n",
    "\n",
    "    This function computes the differential Gröbner basis of an ideal or \n",
    "    an approximation of a full differential ideal if not all derivatives\n",
    "    are known. See Algorithm 4 in the thesis for reference.\n",
    "\n",
    "    # Arguments\n",
    "    - `ideal`: an ideal\n",
    "    - `derivatives`: a dictionary of derivatives\n",
    "    - `R`: a polynomial ring with two blocks of variables, where the first \n",
    "       block corresponds to the variables in `R` that are not in `derivatives` \n",
    "       and the second block corresponds to the variables in `derivatives`\n",
    "    - `R_vars`: the variables in the ring, only needed for elimination\n",
    "    - `nf`: a boolean indicating whether to compute the normal form\n",
    "    - `info_level`: an integer indicating the level of information to print\n",
    "\n",
    "    # Returns\n",
    "    - the differential Gröbner basis\n",
    "\"\"\"\n",
    "function differential_basis(ideal, derivatives, R, R_vars = [], nf=false, info_level=0)\n",
    "    n = R.data.nvars\n",
    "    s = length(derivatives)    \n",
    "    eliminate = n - s\n",
    "\n",
    "    # If elimination is necessary reorganize the ring and substitute the variables\n",
    "    if eliminate > 0\n",
    "        R_new, R_new_vars, map_old_new = _manage_rings(derivatives, R)\n",
    "        \n",
    "        ideal_new_gens = [_swap_vars(elem, R_vars, R_new_vars, map_old_new) for elem in ideal.gens]\n",
    "        ideal = AlgebraicSolving.Ideal(ideal_new_gens)  \n",
    "\n",
    "        derivatives_new = Dict()\n",
    "        for (var, expr) in derivatives\n",
    "            if typeof(expr) == Number\n",
    "                expr = R(expr) # R_new ? TODO\n",
    "            end\n",
    "            derivatives_new[_swap_vars(var, R_vars, R_new_vars, map_old_new)] = _swap_vars(expr, R_vars, R_new_vars, map_old_new)\n",
    "        end\n",
    "        derivatives = derivatives_new\n",
    "\n",
    "        # Infer and create the subring\n",
    "        S_vars = [Symbol(var) for var in R_new_vars[eliminate+1:end]]\n",
    "    else \n",
    "        S_vars = R.data.S\n",
    "    end \n",
    "\n",
    "    # Start computing the differential basis\n",
    "    G1 = groebner_basis(ideal)\n",
    "\n",
    "    pG1 = [_diff_op(g, derivatives) for g in _intersect(G1, S_vars)]\n",
    "    if nf\n",
    "        pG1 = [AlgebraicSolving.normal_form(pg, AlgebraicSolving.Ideal(G1)) for pg in pG1]\n",
    "    end\n",
    "\n",
    "    append!(pG1, G1)\n",
    "    pG1 = Vector{typeof(ideal[1])}(pG1)\n",
    "    G2 = groebner_basis(AlgebraicSolving.Ideal(pG1), eliminate=eliminate,\n",
    "                        intersect=false, info_level=info_level)\n",
    "    if info_level > 0\n",
    "        i = 1\n",
    "        println(\"iteration \", i)\n",
    "        println(\"#G = \", length(G1))\n",
    "    end\n",
    "\n",
    "    # Repeat until closed under _diff_op\n",
    "    while G1 != G2\n",
    "        if info_level > 0\n",
    "            i += 1\n",
    "            println(\"iteration \", i)\n",
    "            println(\"#G = \", length(G2))\n",
    "        end\n",
    "        G1 = G2\n",
    "        pG1 = [_diff_op(g, derivatives) for g in _intersect(G1, S_vars)]\n",
    "        if nf == true\n",
    "            pG1 = [AlgebraicSolving.normal_form(pg, AlgebraicSolving.Ideal(G1)) for pg in pG1]\n",
    "        end\n",
    "        \n",
    "        append!(pG1, G1)\n",
    "        pG1 = Vector{typeof(ideal[1])}(pG1)\n",
    "        G2 = groebner_basis(AlgebraicSolving.Ideal(pG1), eliminate=eliminate,\n",
    "                            intersect=false, info_level=info_level)\n",
    "    end\n",
    "    return G1\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 26,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "(FqMPolyRingElem[x1*x4*Ks + 100*x6, x7^2 + 100*x2], Dict{FqMPolyRingElem, Any}(x3 => 100*x1^4*x7*k1 + x1*x5*k2*K + 100*x3*x5*k2, x7 => dx7, k4 => 0, pCO2 => 0, x4 => 99*x1*x4^2*k3 + x1*x5*k2*K + 100*x3*x5*k2, k1 => 0, K => 0, H => 0, x1 => 99*x1^4*x7*k1 + 100*x1*x4^2*k3 + 100*x1*x5*k2*K + x3*x5*k2, x5 => 100*x1*x5*k2*K + x3*x5*k2 + x6*x7*k4…), Multivariate polynomial ring in 18 variables over GF(101), FqMPolyRingElem[dx6, dx7, x1, x2, x3, x4, x5, x6, x7, k1, k2, k3, k4, K, klA, Ks, pCO2, H])"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "using DifferentialBases\n",
    "i, d, r, rn = DifferentialBases.akzo_nobel()"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 27,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "5-element Vector{FqMPolyRingElem}:\n",
       " x7^2 + 100*x2\n",
       " x1*x4*Ks + 100*x6\n",
       " 51*x1^4*x7*k1 + x1*x4^2*k3 + 51*x6*x7*k4 + 100*klA*pCO2*H + dx7*x7 + x2*klA\n",
       " 51*x1^4*x2*k1 + x1*x4^2*x7*k3 + 100*x7*klA*pCO2*H + 51*x2*x6*k4 + x2*x7*klA + dx7*x2\n",
       " 2*x1^3*x6*x7*k1 + 100*x1^2*x5*k2*K*Ks + x1*x3*x5*k2*Ks + 100*x3*x4*x5*k2*Ks + 2*x1*x4*x6*k3 + x4^2*x6*k3 + x5*x6*k2*K + dx6"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "res = differential_basis(i, d, r, rn)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.10.4",
   "language": "julia",
   "name": "julia-1.10"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.10.4"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
